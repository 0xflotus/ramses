#  -------------------------------------------------------------------------
#  Copyright (C) 2018 BMW Car IT GmbH
#  -------------------------------------------------------------------------
#  This Source Code Form is subject to the terms of the Mozilla Public
#  License, v. 2.0. If a copy of the MPL was not distributed with this
#  file, You can obtain one at https://mozilla.org/MPL/2.0/.
#  -------------------------------------------------------------------------

INCLUDE(FindPackageHandleStandardArgs)
SET(ramses-static-lib_VERSION @RAMSES_VERSION@)

@PACKAGE_INIT@

#tell ramses it's being linked statically
ADD_DEFINITIONS(-DRAMSES_LINK_STATIC)

FIND_PACKAGE(PkgConfig QUIET)

IF (ramses-sdk_INJECT_ROOT_PATH)
    GET_FILENAME_COMPONENT(ramses-sdk_INSTALL_ROOT @PACKAGE_PROJECT_INSTALL_STATIC_LIB@/../.. ABSOLUTE)
    SET(CMAKE_FIND_ROOT_PATH ${CMAKE_FIND_ROOT_PATH} ${ramses-sdk_INSTALL_ROOT})
ENDIF()


################################### client headers ###################################
FIND_PATH(ramses-client_INCLUDE_DIR ramses-client.h
    HINTS @PACKAGE_PROJECT_INSTALL_HEADER@
)

################################### text headers ###################################
FIND_PATH(ramses-text_INCLUDE_DIR ramses-text.h
    HINTS @PACKAGE_PROJECT_INSTALL_HEADER@
)

################################### framework headers ###################################
FIND_PATH(ramses-framework_INCLUDE_DIR RamsesFramework.h
    HINTS @PACKAGE_PROJECT_INSTALL_HEADER@/ramses-framework-api
)

################################### renderer headers ###################################
FIND_PATH(ramses-renderer_INCLUDE_DIR RamsesRenderer.h
    HINTS @PACKAGE_PROJECT_INSTALL_HEADER@/ramses-renderer-api
)

################################### client libraries ###################################
FIND_LIBRARY(ramses-glslang_LIBRARY
    NAMES ramses-glslang
    HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
)

FIND_LIBRARY(ramses-client_LIBRARY
    NAMES ramses-client
    HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
)

################################### text libraries ###################################
FIND_LIBRARY(ramses-text_LIBRARY
    NAMES ramses-text
    HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
)

FIND_LIBRARY(freetype_LIBRARY
    NAMES freetype
    HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
)

FIND_LIBRARY(harfbuzz_LIBRARY
    NAMES harfbuzz
    HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
)

################################### framework libraries ###################################
FIND_LIBRARY(ramses-framework_LIBRARY
    NAMES ramses-framework
    HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
)

FIND_LIBRARY(lz4_LIBRARY
    NAMES lz4
    HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
)

FIND_LIBRARY(ramses-capu_LIBRARIES
    NAMES ramses-capu
    HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
)

# collect required variables
SET(ramses-framework_LIB_REQUIRED_VARS ramses-capu_LIBRARIES ramses-framework_LIBRARY lz4_LIBRARY)

################################### renderer libraries ###################################

# Process COMPONENT parameters

LIST(GET ramses-static-lib_FIND_COMPONENTS 0 RENDERER_PLATFORM)
LIST(LENGTH ramses-static-lib_FIND_COMPONENTS COMPONENTS_LENGTH)
IF(COMPONENTS_LENGTH GREATER 1)
    LIST(GET ramses-static-lib_FIND_COMPONENTS 1 GL_VERSION)
ELSE()
    SET(GL_VERSION NOTFOUND)
ENDIF()

FIND_LIBRARY(ramses-renderer_LIBRARY
    NAMES ramses-renderer-lib
    HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
)

IF("${RENDERER_PLATFORM}" STREQUAL "WINDOWS")

    IF(NOT GL_VERSION)
        MESSAGE("Using OpenGL 4.2 as no OpenGL version was specified")
        SET(GL_VERSION "GL4.2")
    ENDIF()

    IF("${GL_VERSION}" STREQUAL "GL4.2")
        SET(PLATFORM_GL_LIB platform-windows-wgl-4-2-core)
    ELSEIF("${GL_VERSION}" STREQUAL "GL4.5")
        SET(PLATFORM_GL_LIB platform-windows-wgl-4-5)
    ELSEIF("${GL_VERSION}" STREQUAL "GLES3.0")
        SET(PLATFORM_GL_LIB platform-windows-wgl-es-3-0)
    ELSE()
        MESSAGE(FATAL_ERROR "OpenGL version ${GL_VERSION} not supported on platform ${RENDERER_PLATFORM}. Use one of [GL4.2, GL4.5, GLES3.0]")
    ENDIF()

    SET(ramses-renderer-platform-library-names
        ramses-renderer-lib
        ${PLATFORM_GL_LIB}
        Surface_Windows_WGL
        Window_Windows
        Context_WGL
        Device_GL
        EmbeddedCompositor_Dummy)
    # opengl32.lib is just there on windows, skip find_library for it
    SET(ramses-renderer-platform_LIBRARIES opengl32.lib)

ELSEIF("${RENDERER_PLATFORM}" STREQUAL "LINUX")

    IF(NOT GL_VERSION)
        MESSAGE("Using OpenGL ES 3.0 as no OpenGL version was specified")
        SET(GL_VERSION "GLES3.0")
    ENDIF()

    IF("${GL_VERSION}" STREQUAL "GLES3.0")
        SET(PLATFORM_GL_LIB platform-x11-egl-es-3-0)
        SET(DEVICE_GL_LIB GLESv2) # GLESv3 is implemented in GLESv2 (in mesa)
    ELSE()
        MESSAGE(FATAL_ERROR "OpenGL version ${GL_VERSION} not supported on platform ${RENDERER_PLATFORM}. Use one of [GLES3.0]")
    ENDIF()

    SET(ramses-renderer-platform-library-names
        ramses-renderer-lib
        ${PLATFORM_GL_LIB}
        Surface_X11_EGL
        Window_X11
        Context_EGL
        ${DEVICE_GL_LIB}
        Device_GL
        EGL
        X11
        EmbeddedCompositor_Dummy)

ELSE()
    MESSAGE("please use component syntax, choose one of WINDOWS, LINUX")
ENDIF()

# resolve all platform specific libraries in ${ramses-renderer-platform-library-names}
# set none if at least one is not found
FOREACH(lib IN LISTS ramses-renderer-platform-library-names)
    FIND_LIBRARY(${lib}_LIBRARY
        NAMES ${lib}
        HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@)

    IF(${lib}_LIBRARY)
        LIST(APPEND ramses-renderer-platform_LIBRARIES ${${lib}_LIBRARY})
    ELSE()
        MESSAGE(WARNING "Unresolved platform dependency: ${lib}")
        SET(ramses-renderer-platform_LIBRARIES)
        BREAK()
    ENDIF()
ENDFOREACH()

################################### create package from mandatory modules ###################################
FIND_PACKAGE_HANDLE_STANDARD_ARGS(ramses-static-lib
    FOUND_VAR     ramses-static-lib_FOUND
    REQUIRED_VARS ramses-renderer_LIBRARY
                  ramses-renderer_INCLUDE_DIR
                  ramses-renderer-platform_LIBRARIES
                  ramses-framework_INCLUDE_DIR
                  ${ramses-framework_LIB_REQUIRED_VARS}
                  ramses-glslang_LIBRARY
                  ramses-client_LIBRARY
                  freetype_LIBRARY
                  harfbuzz_LIBRARY
                  ramses-client_INCLUDE_DIR
                  ramses-text_INCLUDE_DIR
                  ramses-text_LIBRARY
    VERSION_VAR   ramses-static-lib_VERSION)

SET(ramses-static-lib_INCLUDE_DIRS
    ${ramses-client_INCLUDE_DIR}
    ${ramses-text_INCLUDE_DIR}
    ${ramses-framework_INCLUDE_DIR}
    ${ramses-renderer_INCLUDE_DIR}
)

SET(ramses-static-lib_LIBRARIES
    ${ramses-renderer-platform_LIBRARIES}
    ${ramses-renderer_LIBRARY}
    ${ramses-framework_LIBRARY}
    ${ramses-capu_LIBRARIES}
    ${lz4_LIBRARY}
    ${ramses-client_LIBRARY}
    ${ramses-text_LIBRARY}
    ${ramses-glslang_LIBRARY}
    ${harfbuzz_LIBRARY}
    ${freetype_LIBRARY}
)

################################### append optional modules to package if found ###################################

#detect VTune (optional)
SET(ramses-sdk_check_vtune_library_name @ramses-sdk_VTUNE_LIBRARY_NAME@)
IF (ramses-sdk_check_vtune_library_name)
    IF(NOT ramses-sdk_BUILD_PERFORMANCE_PROFILER_VTUNE_INSTALL_DIR)
        IF (${CMAKE_SYSTEM_NAME} MATCHES "Linux")
            SET(ramses-sdk_BUILD_PERFORMANCE_PROFILER_VTUNE_INSTALL_DIR "/opt/intel/")
        ELSEIF (${CMAKE_SYSTEM_NAME} MATCHES "Windows")
            SET(ramses-sdk_BUILD_PERFORMANCE_PROFILER_VTUNE_INSTALL_DIR "C:/Program Files (x86)/IntelSWTools/VTune Amplifier XE 2017" )
        ENDIF()
    ENDIF()

    IF(CMAKE_SIZEOF_VOID_P MATCHES "8")
        SET(VTune_LIB_DIR lib64)
    ELSE()
        SET(VTune_LIB_DIR lib32)
    ENDIF()

    FIND_LIBRARY(VTune_LIBRARIES
        @ramses-sdk_VTUNE_LIBRARY_NAME@
        PATHS ${ramses-sdk_BUILD_PERFORMANCE_PROFILER_VTUNE_INSTALL_DIR}
        PATH_SUFFIXES ${VTune_LIB_DIR})

    IF (VTune_LIBRARIES)
        IF (${CMAKE_SYSTEM_NAME} MATCHES "Linux")
            LIST(APPEND ramses-static-lib_LIBRARIES dl)
        ENDIF()
        LIST(APPEND ramses-static-lib_LIBRARIES ${VTune_LIBRARIES})
    ELSE()
        MESSAGE(FATAL_ERROR "ramses compiled with VTune but VTune lib not found")
    ENDIF()
ENDIF()

# detect dlt (optional, not needed on Integrity)
IF (NOT "${CMAKE_SYSTEM_NAME}" MATCHES "Integrity")
    SET(ramses-framework_LIB_REQUIRED_VARS ${ramses-framework_LIB_REQUIRED_VARS} automotive-dlt_LIBRARIES)

    IF(PKG_CONFIG_FOUND)
        PKG_CHECK_MODULES(automotive-dlt automotive-dlt QUIET)
    ENDIF()

    IF (NOT automotive-dlt_FOUND)
        # try search regular lib, may find own one
        FIND_LIBRARY(automotive-dlt_LIBRARIES
            NAMES automotive-dlt
            HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
        )
    ENDIF()
    IF (automotive-dlt_LIBRARIES)
        LIST(APPEND ramses-static-lib_LIBRARIES ${automotive-dlt_LIBRARIES})
    ENDIF()
ENDIF()


# try link system dlt library (optional)
FIND_LIBRARY(dlt_LIBRARY NAMES dlt)

IF(dlt_LIBRARY)
    LIST(APPEND ramses-static-lib_LIBRARIES ${dlt_LIBRARY})
ENDIF()

# try link ivi-extension library
#  system libs
FIND_LIBRARY(wayland-ivi-extension_ivi-application_LIBRARY
    NAMES ivi-application
    )
FIND_LIBRARY(wayland-ivi-extension_ivi-controller_LIBRARY
    NAMES ivi-controller
    )
#  own lib
FIND_LIBRARY(wayland-ivi-extension_ramses_LIBRARY
    NAMES wayland-ivi-extension
    HINTS @PACKAGE_PROJECT_INSTALL_STATIC_LIB@
    )

IF (wayland-ivi-extension_ivi-application_LIBRARY AND wayland-ivi-extension_ivi-controller_LIBRARY)
    LIST(APPEND ramses-static-lib_LIBRARIES
        ${wayland-ivi-extension_ivi-application_LIBRARY}
        ${wayland-ivi-extension_ivi-controller_LIBRARY})
ELSEIF(wayland-ivi-extension_ramses_LIBRARY)
    LIST(APPEND ramses-static-lib_LIBRARIES
        ${wayland-ivi-extension_ramses_LIBRARY})
ENDIF()

################################### finalize package ###################################
message(STATUS "Found ramses-static-lib libs: ${ramses-static-lib_LIBRARIES}")
message(STATUS "Found ramses-static-lib includes: ${ramses-static-lib_INCLUDE_DIRS}")

MARK_AS_ADVANCED(
    ramses-static-lib_INCLUDE_DIRS
    ramses-static-lib_LIBRARIES
    ramses-static-lib_FOUND
)
