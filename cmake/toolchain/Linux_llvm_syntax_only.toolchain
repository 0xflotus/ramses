#  -------------------------------------------------------------------------
#  Copyright (C) 2018 BMW Car IT GmbH
#  -------------------------------------------------------------------------
#  This Source Code Form is subject to the terms of the Mozilla Public
#  License, v. 2.0. If a copy of the MPL was not distributed with this
#  file, You can obtain one at https://mozilla.org/MPL/2.0/.
#  -------------------------------------------------------------------------

SET(CMAKE_C_COMPILER   clang)
SET(CMAKE_CXX_COMPILER clang++)

# create fake script to replace linker, archiver, ...
# this is required, since cmake will try linking,
# but there no object files available in this build config.
FILE(WRITE ${CMAKE_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/noop.sh
    "#!/bin/bash\n"
    "echo \"[Skipping build tool invocation]\"\n"
    "libname=`echo \"\$@\" | tr ' ' '\\n' | egrep '^lib.*\\.a\$'`\n"
    "if [ ! \"\$libname\" == \"\" ]; then\n"
    "    touch \$libname\n"
    "fi"
)

# can not apply execute rights in FILE(WRITE ...)
# so copy with execute rights
FILE(COPY             ${CMAKE_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/noop.sh
     DESTINATION      ${CMAKE_BINARY_DIR}
     FILE_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE
)

# now reset all tools invokations that are not required for
# source code checking to NOOP script
SET(CMAKE_AR                  "${CMAKE_BINARY_DIR}/noop.sh" CACHE STRING "")
SET(CMAKE_LINKER              "${CMAKE_BINARY_DIR}/noop.sh" CACHE STRING "")
SET(CMAKE_CXX_LINK_EXECUTABLE "${CMAKE_BINARY_DIR}/noop.sh" CACHE STRING "")
SET(CMAKE_CXX_CREATE_SHARED_LIBRARY "${CMAKE_BINARY_DIR}/noop.sh" CACHE STRING "")
SET(CMAKE_NM                  "${CMAKE_BINARY_DIR}/noop.sh" CACHE STRING "")
SET(CMAKE_OBJCOPY             "${CMAKE_BINARY_DIR}/noop.sh" CACHE STRING "")
SET(CMAKE_OBJDUMP             "${CMAKE_BINARY_DIR}/noop.sh" CACHE STRING "")
SET(CMAKE_RANLIB              "${CMAKE_BINARY_DIR}/noop.sh" CACHE STRING "")
SET(CMAKE_STRIP               "${CMAKE_BINARY_DIR}/noop.sh" CACHE STRING "")

# filter out almost all cmake dependency checking
INCLUDE_REGULAR_EXPRESSION("^MATCH_NOTHING$" "^NEVER_GENERATE_WARNING$")
